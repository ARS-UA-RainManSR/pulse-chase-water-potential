model{
  for(i in 1:N) {
    # Likelihood
    vwc[i] ~ dnorm(mu[i], tau)
    # Replicated
    vwc.rep[i] ~ dnorm(mu[i], tau)
    # Squared differences
    sqdiff[i] <- pow(vwc[i] - vwc.rep[i], 2)
    # Residuals
    resid[i] <- mu[i] - vwc[i]
    
    # Ricker model
    mu[i] <- c[variable[i], period[i], trt[i]] + a[variable[i], period[i], trt[i]] * dop[i] * exp(-b[variable[i], period[i], trt[i]] * dop[i])
  }
  
  # Loop through wp/rwc and pd/md
  for(j in 1:2) {
    for(k in 1:2) {
      for(l in 1:3) {
    
        # Priors for Ricker parameters
        c[j,k,l] ~ dnorm(0, 0.001)
        a[j,k,l] ~ dnorm(0, 0.001)
        b[j,k,l] ~ dgamma(0.1, 0.1)
      
        # Calculated values
        peak.x[j,k,l] <- 1/b[j,k,l]
        peak.y[j,k,l] <- a[j,k,l]/b[j,k,l]*exp(-1) +c[j,k,l]
      }
    }
  }
  
  # Variance terms
  tau ~ dgamma(0.01, 0.01)
  sig <- pow(tau, -0.5)
  
  # Dsum
  Dsum <- sum(sqdiff[])
  # Bayesian R2
  # Compute Bayesian R2 value
  var.pred <- pow(sd(mu[]),2)
  var.resid <- 1/tau
  R2 <- var.pred/(var.pred + var.resid)
 
}